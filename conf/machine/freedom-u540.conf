#@TYPE: Machine
#@NAME: freedom-u540
#@SOC: Freedom U540
#@DESCRIPTION: Machine configuration for the HiFive Unleashed development board

require include/riscv-common.inc

#============================================
# Preferred Providers
PREFERRED_PROVIDER_virtual/kernel ?= "linux-mainline"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot"
#============================================

#============================================
# SBI Configuration
RISCV_SBI_PLAT = "sifive/fu540"
#- This sets u-boot as the default OpenSBI payload
RISCV_SBI_FDT ?= "hifive-unleashed-a00.dtb"
#- This will set the kernel as the OpenSBI payload. This is not recommended,
#-  you can use U-Boot's MMC loading instead.
# RISCV_SBI_PAYLOAD ?= "${KERNEL_IMAGETYPE}-${MACHINE}.bin"
#- Use this to add Microsemi Expansion board support
# RISCV_SBI_FDT ?=  "hifive-unleashed-a00-microsemi.dtb"
#============================================

#============================================
# Kernel Configuration
KERNEL_DEVICETREE ?= "sifive/${RISCV_SBI_FDT}"
SERIAL_CONSOLES = "115200;ttySIF0"
#============================================

#============================================
# Image Configuration
EXTRA_IMAGEDEPENDS += "u-boot"
IMAGE_FSTYPES:remove = "wic.bmap"
IMAGE_BOOT_FILES = " \
    fw_payload.bin \
    fitImage \
    boot.scr.uimg \
"
WKS_FILE ?= "freedom-u540-opensbi.wks"
#============================================

#============================================
# Uboot Configuration
UBOOT_MACHINE = "sifive_unleashed_defconfig"
UBOOT_ENTRYPOINT = "0x80200000"
UBOOT_DTB_LOADADDRESS = "0x82200000"
#- Set this to "mmc-boot" to generate a boot.scr file which should be included
#-  in the boot partition. It will try to load a kernel image by TFTP and if that
#-  fails it will fall back to local images in the boot partition.
UBOOT_ENV ?= "tftp-mmc-boot"
#============================================
